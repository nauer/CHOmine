group 'org.intermine'
version '2.0.0-RC-SNAPSHOT'

buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.0"
        classpath "org.jfrog.buildinfo:build-info-extractor-gradle:4.6.2"
    }
}

apply plugin: 'groovy'
apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'com.jfrog.bintray'
apply plugin: "com.jfrog.artifactory"

sourceCompatibility = 1.7

repositories {
    mavenLocal()
    jcenter()
}

dependencies {
    compile gradleApi()
    compile localGroovy()
}

artifactory {
    contextUrl = 'https://oss.jfrog.org/artifactory/'
    resolve {
        repository {
            repoKey = 'libs-release'
        }
    }

    publish {
        repository {
            if (project.version.endsWith("-SNAPSHOT")) {
                repoKey = 'oss-snapshot-local'
            } else {
                repoKey = 'oss-release-local'
            }
            username = System.getenv('BINTRAY_USER')
            password = System.getenv('BINTRAY_KEY')
        }

        defaults {
            publishConfigs('archives')
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')

    pkg {
        repo = 'intermine'
        name = project.name
        userOrg = user
        licenses = ['LGPL-2.1']
        vcsUrl = 'https://github.com/intermine/intermine.git'
    }

    configurations = ['archives']
}
